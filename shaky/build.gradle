import com.linkedin.gradle.DistributeTask

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = GROUP_ID

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    api 'com.squareup:seismic:1.0.2'
    implementation "androidx.appcompat:appcompat:$supportLibrariesVersion"
    implementation "androidx.recyclerview:recyclerview:$supportLibrariesVersion"
    implementation "androidx.legacy:legacy-support-v4:$supportLibrariesVersion"
    implementation "androidx.annotation:annotation:$supportLibrariesVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'org.robolectric:robolectric:3.0'
}

version = VERSION_NAME

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        aar(MavenPublication) {

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                builtBy assemble
            }
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description = LIBRARY_DESCRIPTION
                    url 'https://github.com/linkedin/shaky-android'
                    scm {
                        url 'https://github.com/linkedin/shaky-android'
                        connection 'scm:git:git://github.com/linkedin/shaky-android.git'
                        developerConnection 'https://github.com/linkedin/shaky-android.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'com.linkedin'
                            name 'LinkedIn Corp.'
                            email ''
                        }
                    }

                    dependencies {
                        configurations.compile.allDependencies.each { ModuleDependency dp ->
                            dependency {
                                resolveStrategy = Closure.DELEGATE_ONLY
                                groupId dp.group
                                artifactId dp.name
                                version dp.version
                            }
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://linkedin.jfrog.io/linkedin'
    publish {
        repoKey = 'shaky-android'
        username = System.getenv('ARTIFACTORY_USER') ?: ''
        password = System.getenv('ARTIFACTORY_KEY') ?: ''

        defaults {
            publications ('aar')
            publishArtifacts = true
        }
    }
    clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: DistributeTask) {
    dependsOn 'artifactoryPublish'
}
