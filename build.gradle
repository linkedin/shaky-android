import com.linkedin.gradle.DistributeTask

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.8.1"
    }
}

subprojects {
    plugins.apply('checkstyle')

    repositories {
        google()
        jcenter()
    }

    tasks.withType(Javadoc).all { enabled = false }
}

ext {
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    minSdkVersion = 21
    targetSdkVersion = 27
}

apply plugin: 'com.jfrog.artifactory'

artifactory {
    contextUrl = 'https://linkedin.jfrog.io/linkedin'
    publish {
        repoKey = 'shaky-android'
        username = System.getenv('ARTIFACTORY_USER') ?: ''
        password = System.getenv('ARTIFACTORY_KEY') ?: ''

        defaults {
            publications ('aar')
            publishArtifacts = true
        }
    }
    clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: DistributeTask) {
    dependsOn ':artifactoryPublish', 'shaky:artifactoryPublish'
}